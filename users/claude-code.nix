{
  inputs,
  lib,
  config,
  pkgs,
  ...
}:

{
  config = {
    home.sessionPath = [ "$HOME/.local/bin" ]; # `claude install` puts the cli here

    home.packages = with pkgs; [
      csharp-ls
      jq
    ];

    # Create marketplace structure
    home.file.".claude-plugins/.claude-plugin/marketplace.json".text = builtins.toJSON {
      name = "local-plugins";
      owner.name = "Local Configuration";
      plugins = [
        {
          name = "csharp-lsp";
          source = "./plugins/csharp-lsp";
          description = "C# language server providing code intelligence and diagnostics";
        }
      ];
    };

    # Create the plugin
    home.file.".claude-plugins/plugins/csharp-lsp/.claude-plugin/plugin.json".text = builtins.toJSON {
      name = "csharp-lsp";
      version = "1.0.0";
      description = "C# language server providing code intelligence and diagnostics";
      author.name = "Local Configuration";
      lspServers = "./.lsp.json";
    };

    home.file.".claude-plugins/plugins/csharp-lsp/.lsp.json".text = builtins.toJSON {
      csharp = {
        command = "${pkgs.csharp-ls}/bin/csharp-ls";
        args = [ ];
        extensionToLanguage = {
          ".cs" = "csharp";
        };
        restartOnCrash = true;
        maxRestarts = 5;
      };
    };

    # Install claude via native installer if not present
    home.activation.installClaude = lib.hm.dag.entryAfter [ "writeBoundary" ] ''
      if [ ! -x "$HOME/.local/bin/claude" ]; then
        run ${pkgs.curl}/bin/curl -fsSL https://claude.ai/install.sh | ${pkgs.bash}/bin/bash
      fi
    '';

    # Patch settings.json declaratively while allowing Claude Code to manage it mutably
    # This uses a shell script to merge our declarative settings with Claude's runtime settings
    home.activation.patchClaudeSettings = lib.hm.dag.entryAfter [ "writeBoundary" ] ''
      run ${pkgs.bash}/bin/bash ${./patch-claude-settings.sh}
    '';

    home.file.".claude/CLAUDE.md".text = ''
    - commit regularly to checkpoint changes
    - add co-authored-by to commit messages but do not add 'Generated by Claude Code'
    - use gh cli to read contents of issues and pull requests. fetch both description and comments
    - do not add github issue references to commit messages
    '';

    # Skills
    home.file.".claude/skills/pr-review-comments/SKILL.md".source = ./claude-code/pr-review-comments.md;
  };
}
